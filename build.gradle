buildscript {
  ext.kotlinVersion = '1.2.0'

  repositories {
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
  }

  dependencies {
    classpath 'com.gradle.publish:plugin-publish-plugin:0.9.9'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.11.0'
  }
}

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'com.vanniktech.android.junit.jacoco'

repositories {
  jcenter()
  google()
  maven { url "https://plugins.gradle.org/m2/" }
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile 'com.android.tools.build:gradle:3.0.1'
  compile 'de.aaschmid:gradle-cpd-plugin:1.1'
  compile 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.13'

  testCompile 'junit:junit:4.12'
  testCompile "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
}

sourceCompatibility = JavaVersion.VERSION_1_7

pluginBundle {

  plugins {
    codeQualityTools {
      id = 'com.vanniktech.code.quality.tools'
      tags = ['gradle', 'android', 'java', 'findbugs', 'pmd', 'cpd', 'detekt', 'kotlin', 'ktlint', 'checkstyle', 'errorprone', 'multiprojects', 'app module', 'library module', 'lint']
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.4.1'
  distributionType = Wrapper.DistributionType.ALL
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
  classifier = 'groovydoc'
  from 'build/docs/groovydoc'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

artifacts {
  archives jar
  archives groovydocJar
  archives javadocJar
  archives sourcesJar
}

apply plugin: 'maven-publish'

publishing {
  publications {
    main(MavenPublication) {
      from components.java
      artifact sourcesJar
    }
  }
}